package edu.robertob.p1compi2.analysis;

import java_cup.runtime.Symbol;
import java.util.LinkedList;
import java.util.List;
import java.util.HashMap;

import edu.robertob.p1compi2.engine.structs.*;
import edu.robertob.p1compi2.engine.base.*;
import edu.robertob.p1compi2.engine.statements.*;
import edu.robertob.p1compi2.engine.expressions.*;


parser code
{:
    PLexer s;
    PParser(PLexer s){this.s = s;}
    private LinkedList<PError> errorList = new LinkedList<>();
    public LinkedList<PError> getSyntaxErrorList(){
        return errorList;
    }
//
    public void syntax_error(Symbol s){
        errorList.add(new PError("✍ Sintaxis", "No se esperaba el componente: "+s.value, s.left, s.right));
//          System.out.println("Syntax error at line: "+s.left+" column: "+s.right + " at symbol: "+s.value);
//          System.out.println(expected_token_ids());
    }
//
    public void unrecovered_syntax_error (Symbol s){
        errorList.add(new PError("✍ ERROR FATAL - Deteniendo...", "No se esperaba el componente: "+s.value, s.left, s.right));
//        System.out.println("Unrecovered syntax error at line: "+s.left+" column: "+s.right + " at symbol: "+s.value);
//        System.out.println(expected_token_ids());
    }

//    public void report_error(String message, Object info){
//        errorList.add(new PError("Sintaxis", message);
//        System.out.println("info: "+info + " message: "+message);
//    }

:}

action code
{::}

// Reserverd words
terminal RW_AND, RW_ARRAY, RW_BEGIN, RW_CASE, RW_CONST, RW_DIV, RW_DO, RW_DOWNTO, RW_ELSE, RW_END, RW_FILE, RW_FOR, RW_FUNCTION, RW_GOTO, RW_IF, RW_IN, RW_LABEL, RW_MOD, RW_NIL, RW_NOT, RW_OF, RW_OR, RW_PACKED, RW_PROCEDURE, RW_PROGRAM, RW_RECORD, RW_REPEAT, RW_SET, RW_THEN, RW_TO, RW_TYPE, RW_UNTIL, RW_VAR, RW_WHILE, RW_WITH, RW_TRUE, RW_FALSE, RW_INTEGER, RW_REAL, RW_CHAR, RW_STRING, RW_BOOLEAN, RW_AND_THEN, RW_OR_ELSE, RW_BREAK, RW_CONTINUE, RW_WRITELN, RW_READLN;

terminal UNIT_MINUS;
terminal String STRING, ID, INTEGER, DECIMAL, CHAR;
terminal PLUS, MINUS, EQUALS, OPENBRACE, CLOSEBRACE, ASTERISK, SLASH, MODULO, DOUBLEASTERISK, NEGATION, COLON;
terminal DOUBLEEQUALS, LESS, GREATER, LESSEQUAL, GREATEREQUAL, NOT_EQUALS, LTGT;
terminal OPENPAR, CLOSEPAR;
terminal OR, AND, NOT;
terminal COMMA, SEMICOLON, OPENBRACKET, CLOSEBRACKET, DOT, UNDERSCORE, ARROW;

nonterminal Program START;
nonterminal Program PROGRAM;
nonterminal Program.ProgramHeader PROGRAM_HEADER;
nonterminal Statement PROGRAM_BODY_STATEMENT;
nonterminal LinkedList<Statement> PROGRAM_BODY;

//nonterminal Statement DECLARATIONS_BLOCK;
nonterminal Object[] DECLARATIONS_BLOCK;
nonterminal LinkedList<TypeDeclaration> TYPE_DECLARATION_BLOCK;
nonterminal LinkedList<TypeDeclaration> TYPE_DECLARATION_LIST;

nonterminal Statement TYPE_DECLARATION;
nonterminal LinkedList<String> IDENTIFIER_LIST;

nonterminal TypeDeclaration.ArrayTypeDeclarationHelper ARRAY_TYPE;
nonterminal Integer RANGE_INT;
nonterminal Object[] RANGE_DEF;
nonterminal Statement RECORD_DECLARATION, FIELD_LIST, FIELD_DECLARATION, RECORD_USE;


nonterminal LinkedList<Statement> CONSTANT_DECLARATION_BLOCK, CONSTANT_DECLARATION_LIST;
nonterminal Statement CONSTANT_VALUE, CONSTANT_DECLARATION;

nonterminal LinkedList<Statement> VARIABLE_DECLARATION_BLOCK, VARIABLE_DECLARATION_LIST;
nonterminal Statement VARIABLE_DECLARATION;
nonterminal Integer TYPE;


nonterminal Statement EXPRESSION;

nonterminal Statement STATEMENTS, STATEMENT;
nonterminal Statement SYMBOL_ASSIGNATION;

nonterminal Statement IF_STATEMENT;
nonterminal LinkedList<IfStatement.ConditionBlock> ELSE_IF_BLOCKS;
nonterminal IfStatement.ConditionBlock ELSE_IF_BLOCK;
nonterminal LinkedList<Statement> opt_else_part, statement_or_program_body;

nonterminal Statement CASE_STATEMENT, CASE_ELEMENT_LIST, CASE_ELEMENT, CASE_VALUE_LIST, CASE_ELSE, CASE_VALUE;

nonterminal Statement WHILE_STATEMENT, REPEAT_STATEMENT, FOR_STATEMENT;

nonterminal Statement FUNCTION_DECLARATION_BLOCK;
nonterminal Object[] FUNCTION_DECLARATION_START;
nonterminal LinkedList<HashMap> PARAMETERS_DECLARATION;
nonterminal HashMap<String, Object> PARAMETER_DECLARATION;
nonterminal Statement PROCEDURE_DECLARATION_BLOCK;
nonterminal Object[] PROCEDURE_DECLARATION_START;
nonterminal Statement WRITELN_CALL, READLN_CALL;
nonterminal Statement PROCEDURE_FUNCTION_DECLARATION;
nonterminal LinkedList<Statement> PROCEDURE_FUNCTION_DECLARATIONS;

nonterminal Statement CALL_TO_METHOD, CALL_ARGUMENTS, ARGUMENT;

// Precedence: not > and > or > or else, and then
precedence left RW_OR_ELSE, RW_AND_THEN;
precedence left RW_OR;
precedence left RW_AND;
precedence left RW_NOT;
precedence left EQUALS, LESS, GREATER, LESSEQUAL, GREATEREQUAL, LTGT;
precedence left PLUS, MINUS;
precedence left ASTERISK, RW_DIV, RW_MOD;
precedence left RW_ELSE;
precedence right UNIT_MINUS;

start with START;

START ::= PROGRAM:a DOT {: RESULT = a; :}
;

PROGRAM ::=
    // with functions and body
    PROGRAM_HEADER:a PROCEDURE_FUNCTION_DECLARATIONS:c RW_BEGIN PROGRAM_BODY:b RW_END {:
        RESULT = new Program(a,c,b);
    :}
    // only body
    |
    PROGRAM_HEADER:a RW_BEGIN PROGRAM_BODY:b RW_END {:
        RESULT = new Program(a,null,b);
    :}
    |
    // only functions
    PROGRAM_HEADER:a PROCEDURE_FUNCTION_DECLARATIONS:c RW_BEGIN RW_END {:
        RESULT = new Program(a,c,null);
    :}
    |
    // no functions, no body
    PROGRAM_HEADER:a RW_BEGIN RW_END {:
        RESULT = new Program(a,null,null);
    :}
;

PROCEDURE_FUNCTION_DECLARATIONS ::= PROCEDURE_FUNCTION_DECLARATIONS:a PROCEDURE_FUNCTION_DECLARATION:b {: RESULT = a; a.add(b); :}
                                  | PROCEDURE_FUNCTION_DECLARATION:a {: RESULT = new LinkedList<>(); RESULT.add(a); :}
                                  ;

PROCEDURE_FUNCTION_DECLARATION ::= FUNCTION_DECLARATION_BLOCK:a {: RESULT = a; :}
                            | PROCEDURE_DECLARATION_BLOCK
                            ;

PROGRAM_HEADER ::=
    RW_PROGRAM ID:a SEMICOLON DECLARATIONS_BLOCK:b {:
    RESULT = new Program.ProgramHeader(aleft, aright, a);
    var typesList = (LinkedList<TypeDeclaration>)b[0];
    var constsList = (LinkedList<Statement>)b[1];
    var varsList = (LinkedList<Statement>)b[2];
    if (typesList == null) typesList = new LinkedList<>();
    if (constsList == null) constsList = new LinkedList<>();
    if (varsList == null) varsList = new LinkedList<>();

    RESULT.getStatements().addAll(typesList);
    RESULT.getStatements().addAll(constsList);
    RESULT.getStatements().addAll(varsList);
    System.out.println("Program header: "+RESULT.getStatements().size());
    :}
;

DECLARATIONS_BLOCK ::=
                TYPE_DECLARATION_BLOCK:typesList CONSTANT_DECLARATION_BLOCK:constsList VARIABLE_DECLARATION_BLOCK:varsList
                {: RESULT = new Object[]{typesList, constsList, varsList}; System.out.println("Declarations block: "+typesList.size()+" "+constsList.size()+" "+varsList.size()); :}
                ;

/* this production is for the main program block
because we need to handle special things like semicolons inside if blocks, etc.
*/
PROGRAM_BODY ::= PROGRAM_BODY:a PROGRAM_BODY_STATEMENT:b {: RESULT = a; a.add(b); :}
               | PROGRAM_BODY_STATEMENT:a {: RESULT = new LinkedList<>(); RESULT.add(a); :}
               ;

PROGRAM_BODY_STATEMENT ::= SYMBOL_ASSIGNATION:a SEMICOLON {: RESULT = a; :}
                            | IF_STATEMENT:a {: RESULT = a; :}
                            | WHILE_STATEMENT:a {: RESULT = a; :}
                            | REPEAT_STATEMENT:a {: RESULT = a; :}
                            | FOR_STATEMENT:a {: RESULT = a; :}
                            | RW_BREAK:a SEMICOLON {: RESULT = new Break(aleft, aright); :}
                            | RW_CONTINUE:a SEMICOLON {: RESULT = new Continue(aleft, aright); :}
                            | CALL_TO_METHOD SEMICOLON
                            | CASE_STATEMENT
                            | WRITELN_CALL SEMICOLON
                            | READLN_CALL SEMICOLON
                            ;
//
//STATEMENTS ::= STATEMENTS SEMICOLON STATEMENT
//             | STATEMENT
//             | /* empty */
//             ;

STATEMENT ::= SYMBOL_ASSIGNATION:a {: RESULT = a; :}
            | IF_STATEMENT:a {: RESULT = a; :}
            | CASE_STATEMENT
            | WHILE_STATEMENT
            | REPEAT_STATEMENT
            | FOR_STATEMENT
            | RW_BREAK
            | RW_CONTINUE
            | CALL_TO_METHOD
            | WRITELN_CALL
            | READLN_CALL
            ;

/*
Type definitions
*/

TYPE_DECLARATION_BLOCK ::= RW_TYPE TYPE_DECLARATION_LIST:a {: RESULT = a; :}
                          | /* empty */ {: RESULT = new LinkedList<>(); :}
;


TYPE_DECLARATION_LIST ::= TYPE_DECLARATION:a {: RESULT = new LinkedList<>(); RESULT.add((TypeDeclaration)a); :}
                        | TYPE_DECLARATION:a SEMICOLON {: RESULT = new LinkedList<>(); RESULT.add((TypeDeclaration)a); :}
                        | TYPE_DECLARATION:a SEMICOLON TYPE_DECLARATION_LIST:b {: RESULT = b; b.add((TypeDeclaration)a); :}
                        ;

//TYPE_DECLARATION_LIST ::= TYPE_DECLARATION_LIST SEMICOLON TYPE_DECLARATION
//                        | TYPE_DECLARATION
//                        | /* empty */
//                        ;

TYPE_DECLARATION ::= IDENTIFIER_LIST:a EQUALS TYPE:b {: RESULT = new TypeDeclaration(aleft, aright, a, b, false, false, false, 1, 0, 0); :}
                   | IDENTIFIER_LIST:a EQUALS ARRAY_TYPE:b {: RESULT = new TypeDeclaration(aleft, aright, a, b.getParentTypeId(), true, false, false, ((int)b.getRangeDefinition()[1] - (int)b.getRangeDefinition()[0])+1, (int)b.getRangeDefinition()[0], (int)b.getRangeDefinition()[1]); :}
                   | IDENTIFIER_LIST:a EQUALS RANGE_DEF:b {: RESULT = new TypeDeclaration(aleft, aright, a, TypesTable.DefaultTypes.INTEGER.id, false, true, false, 1, (int)b[0], (int)b[1]); :}
                   | IDENTIFIER_LIST:a EQUALS /*RW_RECORD*/ ID:b {: RESULT = new TypeDeclaration(aleft, aright, a, b, false, false, true, 0, 0, 0); :}
                   | RECORD_DECLARATION
                   ;


IDENTIFIER_LIST ::= IDENTIFIER_LIST:a COMMA ID:b {: RESULT = a; a.add(b); :}
                  | ID:a {: RESULT = new LinkedList<>(); RESULT.add(a); :}
                  ;

TYPE ::= RW_INTEGER:a {: RESULT = TypesTable.DefaultTypes.INTEGER.id; :}
         | RW_REAL:a {: RESULT = TypesTable.DefaultTypes.REAL.id; :}
         | RW_CHAR:a {: RESULT = TypesTable.DefaultTypes.CHARACTER.id; :}
         | RW_STRING:a {: RESULT = TypesTable.DefaultTypes.STRING.id; :}
         | RW_BOOLEAN:a {: RESULT = TypesTable.DefaultTypes.BOOLEAN.id; :}
         ;

/*
Range and array types
*/
RANGE_INT ::= INTEGER:a {: RESULT = Integer.parseInt(a); :}
            | MINUS INTEGER:a {: RESULT = Integer.parseInt(a) * -1; :}
            ;

RANGE_DEF ::= RANGE_INT:min DOT DOT RANGE_INT:max {: RESULT = new Object[]{min, max}; :}
            | CHAR:min DOT DOT CHAR:max {: RESULT = new Object[]{(int)min.charAt(0), (int)max.charAt(0)}; :}
             ;

ARRAY_TYPE ::=
    RW_ARRAY OPENBRACKET RANGE_DEF:a CLOSEBRACKET RW_OF TYPE:b {: RESULT = new TypeDeclaration.ArrayTypeDeclarationHelper(a, b); :}
  | RW_PACKED RW_ARRAY OPENBRACKET RANGE_DEF:a CLOSEBRACKET RW_OF TYPE:b {: RESULT = new TypeDeclaration.ArrayTypeDeclarationHelper(a, b); :}
;

/*
Record type definitions
*/

RECORD_DECLARATION ::= IDENTIFIER_LIST EQUALS RECORD_USE
                   ;

RECORD_USE ::= RW_RECORD FIELD_LIST RW_END
                    ;

FIELD_LIST ::= FIELD_DECLARATION
            | FIELD_DECLARATION SEMICOLON
            | FIELD_DECLARATION SEMICOLON FIELD_LIST
            ;


FIELD_DECLARATION ::= IDENTIFIER_LIST COLON TYPE
                    | IDENTIFIER_LIST COLON ARRAY_TYPE
                    | IDENTIFIER_LIST COLON RANGE_DEF
                    | IDENTIFIER_LIST COLON RW_RECORD ID
                    ;

/*
const and var definitions
*/

/*
Constant Declarations
*/
CONSTANT_DECLARATION_BLOCK ::= RW_CONST CONSTANT_DECLARATION_LIST:a {: RESULT = a; :}
                             | /* empty */ {: RESULT = new LinkedList<>(); :}
;

CONSTANT_DECLARATION_LIST ::= CONSTANT_DECLARATION:a SEMICOLON CONSTANT_DECLARATION_LIST:b {: RESULT = b; b.add(a); :}
                            | CONSTANT_DECLARATION:a {: RESULT = new LinkedList<>(); RESULT.add(a); :}
                            | /* empty */ {: RESULT = new LinkedList<>(); :}
;

// params: int typeId, int line, int column, String id, Statement value
CONSTANT_DECLARATION ::= ID:a EQUALS CONSTANT_VALUE:b {: RESULT = new ConstantDeclaration(-1, aleft, aright, a, b); :}
;

CONSTANT_VALUE ::= INTEGER:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.INTEGER.id, Integer.parseInt(a)); :}
                 | DECIMAL:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.REAL.id, Double.parseDouble(a)); :}
                 | CHAR:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.CHARACTER.id, (int)a.charAt(0)); :}
                 | STRING:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.STRING.id, a); :}
                 | RW_TRUE:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.BOOLEAN.id, true); :}
                 | RW_FALSE:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.BOOLEAN.id, false); :}
                 ;

/*
Variable Declarations
*/

VARIABLE_DECLARATION_BLOCK ::= RW_VAR VARIABLE_DECLARATION_LIST:a {: RESULT = a; :}
                          | /* empty */ {: RESULT = new LinkedList<>(); :}
;


VARIABLE_DECLARATION_LIST ::= VARIABLE_DECLARATION:a SEMICOLON VARIABLE_DECLARATION_LIST:b {: RESULT = b; b.add(a); :}
                         | VARIABLE_DECLARATION:a {: RESULT = new LinkedList<>(); RESULT.add(a); :}
                         | /* empty */ {: RESULT = new LinkedList<>(); :}
;
// params: int typeId, int line, int column, LinkedList<String> ids, Statement value, boolean isArray, boolean isRange, boolean isRecord, Object minVal, Object maxVal, String parentTypeName, int parentTypeId
VARIABLE_DECLARATION ::= IDENTIFIER_LIST:a COLON TYPE:b {: RESULT = new VariableDeclaration(-1, aleft, aright, a, null, false, false, false, null, null, null, b); :}
                    | IDENTIFIER_LIST:a COLON ID:b {: RESULT = new VariableDeclaration(-1, aleft, aright, a, null, false, false, true, null, null, b, -1); :}
                    | IDENTIFIER_LIST:a COLON ARRAY_TYPE:b {: RESULT = new VariableDeclaration(-1, aleft, aright, a, null, true, false, false, b.getRangeDefinition()[0], b.getRangeDefinition()[1], null, b.getParentTypeId()); :}
                    | IDENTIFIER_LIST:a COLON RECORD_USE:b {::}
                    | IDENTIFIER_LIST:a COLON RANGE_DEF:b {: RESULT = new VariableDeclaration(-1, aleft, aright, a, null, false, true, false, b[0], b[1], null, TypesTable.DefaultTypes.INTEGER.id); :}
;

/*
assignations
*/

SYMBOL_ASSIGNATION ::= ID:a COLON EQUALS EXPRESSION:b {: RESULT = new SymbolAssignation(a, b, aleft, aright); :}
                    | ID OPENBRACKET EXPRESSION CLOSEBRACKET COLON EQUALS EXPRESSION
;

/*
if, else if, else statements
*/

IF_STATEMENT ::=
//<editor-fold desc="if else">

  // if (exp) then statement;
  RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN STATEMENT:b SEMICOLON {:
  var statementAsList = new LinkedList<Statement>();
    statementAsList.add(b);
  RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, statementAsList, new LinkedList<IfStatement.ConditionBlock>()), aleft, aright);
  :}

  // if (exp) then begin {statements;} end;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN RW_BEGIN PROGRAM_BODY:b RW_END SEMICOLON {:
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, b, new LinkedList<IfStatement.ConditionBlock>()), aleft, aright);
  :}

  // if (exp) then statement else statement;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN STATEMENT:b RW_ELSE STATEMENT:c SEMICOLON {:
    var statementAsList = new LinkedList<Statement>();
    statementAsList.add(b);

    var statementAsList2 = new LinkedList<Statement>();
    statementAsList2.add(c);
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, statementAsList, new LinkedList<IfStatement.ConditionBlock>()), statementAsList2, aleft, aright);
  :}

  // if (exp) then begin {statements;} end else statement;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN RW_BEGIN PROGRAM_BODY:b RW_END RW_ELSE STATEMENT:c SEMICOLON
    {:
    var statementAsList = new LinkedList<Statement>();
    statementAsList.add(c);
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, b, new LinkedList<IfStatement.ConditionBlock>()), statementAsList, aleft, aright);
    :}

  // if (exp) then begin {statements;} end else begin {statements;} end;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN RW_BEGIN PROGRAM_BODY:b RW_END RW_ELSE RW_BEGIN PROGRAM_BODY:c RW_END SEMICOLON
    {:
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, b, new LinkedList<IfStatement.ConditionBlock>()), c, aleft, aright);
    :}

  // if (exp) then statement else begin {statements;} end;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN STATEMENT:b RW_ELSE RW_BEGIN PROGRAM_BODY:c RW_END SEMICOLON
    {:
    var statementAsList = new LinkedList<Statement>();
    statementAsList.add(b);
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, statementAsList, new LinkedList<IfStatement.ConditionBlock>()), c, aleft, aright);
    :}

  // with else if
  // if (exp) then statement else if (exp) ...;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN STATEMENT:b ELSE_IF_BLOCKS:c SEMICOLON
  {:
  var statementAsList = new LinkedList<Statement>();
    statementAsList.add(b);
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, statementAsList, c), aleft, aright);
  :}

  // if (exp) then begin {statements;} end else if (exp) ...;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN RW_BEGIN PROGRAM_BODY:b RW_END ELSE_IF_BLOCKS:c SEMICOLON
    {:
        RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, b, c), aleft, aright);
    :}

  // if (exp) then statement else if (exp) ... else statement;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN STATEMENT:b ELSE_IF_BLOCKS:c RW_ELSE STATEMENT:d SEMICOLON
    {:
    var statementAsList = new LinkedList<Statement>();
    statementAsList.add(b);

    var statementAsList2 = new LinkedList<Statement>();
    statementAsList2.add(d);
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, statementAsList, c), statementAsList2, aleft, aright);
    :}

  // if (exp) then begin {statements;} else if ... end else statement;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN RW_BEGIN PROGRAM_BODY:b RW_END ELSE_IF_BLOCKS:c RW_ELSE STATEMENT:d SEMICOLON
    {:
    var statementAsList = new LinkedList<Statement>();
    statementAsList.add(d);
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, b, c), statementAsList, aleft, aright);
    :}

  // if (exp) then begin {statements;} end else if ... else begin {statements;} end;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN RW_BEGIN PROGRAM_BODY:b RW_END ELSE_IF_BLOCKS:c RW_ELSE RW_BEGIN PROGRAM_BODY:d RW_END SEMICOLON
    {:
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, b, c), d, aleft, aright);
    :}

  // if (exp) then statement else if ... else begin {statements;} end;
  | RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN STATEMENT:b ELSE_IF_BLOCKS:c RW_ELSE RW_BEGIN PROGRAM_BODY:d RW_END SEMICOLON
    {:
    var statementAsList = new LinkedList<Statement>();
    statementAsList.add(b);
    RESULT = new IfStatement(IfStatement.buildConditionBlockList(a, statementAsList, c), d, aleft, aright);
    :}

//</editor-fold>
;

ELSE_IF_BLOCKS ::= ELSE_IF_BLOCKS:a ELSE_IF_BLOCK:b {: RESULT = a; a.add(b); :}
                 | ELSE_IF_BLOCK:b {: RESULT = new LinkedList<>(); RESULT.add(b); :}
//                 | /* empty */
                 ;

ELSE_IF_BLOCK ::= RW_ELSE RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN STATEMENT:b {:
    var statementAsList = new LinkedList<Statement>();
    statementAsList.add(b);
    RESULT = new IfStatement.ConditionBlock(a, statementAsList);
 :} %prec RW_ELSE
    | RW_ELSE RW_IF OPENPAR EXPRESSION:a CLOSEPAR RW_THEN RW_BEGIN PROGRAM_BODY:b RW_END  {:
        RESULT = new IfStatement.ConditionBlock(a, b);
     :}%prec RW_ELSE
    ;

/*
case statements
*/

CASE_STATEMENT ::=
    // case exp of
    RW_CASE OPENPAR EXPRESSION CLOSEPAR RW_OF CASE_ELEMENT_LIST CASE_ELSE RW_END SEMICOLON
    | RW_CASE OPENPAR EXPRESSION CLOSEPAR RW_OF CASE_ELEMENT_LIST RW_END SEMICOLON
    ;

/* Define the list of case elements */
CASE_ELEMENT_LIST ::= CASE_ELEMENT_LIST CASE_ELEMENT
                    | CASE_ELEMENT
                    ;

/* Define an individual case element */
CASE_ELEMENT ::= CASE_VALUE_LIST COLON STATEMENT SEMICOLON
               | CASE_VALUE_LIST COLON RW_BEGIN PROGRAM_BODY RW_END
               ;

/* Define the possible values for each case */
CASE_VALUE_LIST ::= CASE_VALUE_LIST COMMA EXPRESSION
                  | EXPRESSION
                  ;

/* Define the optional else part of the case statement more explicitly */
CASE_ELSE ::= RW_ELSE STATEMENT SEMICOLON %prec RW_ELSE
            | RW_ELSE RW_BEGIN PROGRAM_BODY RW_END %prec RW_ELSE
            ;

/*
while loop
*/

WHILE_STATEMENT ::= RW_WHILE OPENPAR EXPRESSION:a CLOSEPAR RW_DO STATEMENT:b SEMICOLON {:
    var statementAsList = new LinkedList<Statement>();
    statementAsList.add(b);
    RESULT = new WhileLoop(a, statementAsList, aleft, aright);
    :}

    | RW_WHILE OPENPAR EXPRESSION:a CLOSEPAR RW_DO RW_BEGIN PROGRAM_BODY:b RW_END SEMICOLON {:
    RESULT = new WhileLoop(a, b, aleft, aright);
    :}
                  ;

/*
* repeat until loop
*/

REPEAT_STATEMENT ::= RW_REPEAT PROGRAM_BODY:a RW_UNTIL EXPRESSION:b SEMICOLON
                    {:
                    RESULT = new RepeatLoop(b, a, aleft, aright);
                    :}
                    ;

/*
* for do loop
*/

FOR_STATEMENT ::= RW_FOR ID COLON EQUALS EXPRESSION RW_TO EXPRESSION RW_DO STATEMENT SEMICOLON
                | RW_FOR ID COLON EQUALS EXPRESSION RW_DOWNTO EXPRESSION RW_DO STATEMENT SEMICOLON
                | RW_FOR ID COLON EQUALS EXPRESSION RW_TO EXPRESSION RW_DO RW_BEGIN PROGRAM_BODY RW_END SEMICOLON
                | RW_FOR ID COLON EQUALS EXPRESSION RW_DOWNTO EXPRESSION RW_DO RW_BEGIN PROGRAM_BODY RW_END SEMICOLON
                ;

/*
* function and procedure declarations
*/


FUNCTION_DECLARATION_START ::=
    RW_FUNCTION ID:a OPENPAR PARAMETERS_DECLARATION:b CLOSEPAR COLON TYPE:c SEMICOLON {: RESULT = new Object[]{a, b, c, null}; :}
  | RW_FUNCTION ID:a OPENPAR PARAMETERS_DECLARATION:b CLOSEPAR COLON ID:c SEMICOLON {: RESULT = new Object[]{a, b, -1, c}; :}
  // no params
    | RW_FUNCTION ID:a COLON TYPE:b SEMICOLON {: RESULT = new Object[]{a, new LinkedList<>(), b}; :}
;

// constructor: int typeId, int line, int column, String id, LinkedList<HashMap> params, LinkedList<Statement> header, LinkedList<Statement> methodBody, String returnTypeName, String returnTypeId
FUNCTION_DECLARATION_BLOCK ::= FUNCTION_DECLARATION_START:a DECLARATIONS_BLOCK:b RW_BEGIN PROGRAM_BODY:c RW_END SEMICOLON
{:
var allHeaderStatements = new LinkedList<Statement>();
var typesList = (LinkedList<TypeDeclaration>)b[0];
var constsList = (LinkedList<Statement>)b[1];
var varsList = (LinkedList<Statement>)b[2];
if (typesList == null) typesList = new LinkedList<>();
if (constsList == null) constsList = new LinkedList<>();
if (varsList == null) varsList = new LinkedList<>();
allHeaderStatements.addAll(typesList);
allHeaderStatements.addAll(constsList);
allHeaderStatements.addAll(varsList);

RESULT = new FunctionDeclaration(
    0,
    aleft,
    aright,
    (String)a[0],
    (LinkedList<HashMap>)a[1],
    allHeaderStatements,
    c,
    (String)a[2],
    (int)a[3]
    );
   :}
;


PROCEDURE_DECLARATION_START ::= RW_PROCEDURE ID OPENPAR PARAMETERS_DECLARATION CLOSEPAR SEMICOLON
;
PROCEDURE_DECLARATION_BLOCK ::= PROCEDURE_DECLARATION_START DECLARATIONS_BLOCK RW_BEGIN PROGRAM_BODY RW_END SEMICOLON
;

/*
parameters, examples:

argument0: type1; argument1, argument2: type2; ...
*/
PARAMETERS_DECLARATION ::= PARAMETERS_DECLARATION:a SEMICOLON PARAMETER_DECLARATION:b {: RESULT = a; a.add(b); :}
                         | PARAMETER_DECLARATION:a {: RESULT = new LinkedList<>(); RESULT.add(a); :}
                         ;

PARAMETER_DECLARATION ::=
                        // By value
                        IDENTIFIER_LIST:a COLON TYPE:b {: RESULT = new HashMap<>(); RESULT.put("byRef", false); RESULT.put("ids", a); RESULT.put("type", b); :}
                        | IDENTIFIER_LIST:a COLON ARRAY_TYPE:b {: RESULT = new HashMap<>(); RESULT.put("byRef", false); RESULT.put("ids", a); RESULT.put("type", b.getParentTypeId()); RESULT.put("isArray", true); RESULT.put("isRange", false); RESULT.put("isRecord", false); RESULT.put("minVal", b.getRangeDefinition()[0]); RESULT.put("maxVal", b.getRangeDefinition()[1]); :}
                        | IDENTIFIER_LIST:a COLON RANGE_DEF:b {: RESULT = new HashMap<>(); RESULT.put("byRef", false); RESULT.put("ids", a); RESULT.put("type", TypesTable.DefaultTypes.INTEGER.id); RESULT.put("isArray", false); RESULT.put("isRange", true); RESULT.put("isRecord", false); RESULT.put("minVal", b[0]); RESULT.put("maxVal", b[1]); :}
//                        | IDENTIFIER_LIST COLON /*RW_RECORD*/ ID
                        | IDENTIFIER_LIST:a COLON ID:b {: RESULT = new HashMap<>(); RESULT.put("byRef", false); RESULT.put("ids", a); RESULT.put("type", b); RESULT.put("isArray", false); RESULT.put("isRange", false); RESULT.put("isRecord", false); RESULT.put("minVal", null); RESULT.put("maxVal", null); :}

                        // By reference
                        | RW_VAR IDENTIFIER_LIST:a COLON TYPE:b {: RESULT = new HashMap<>(); RESULT.put("byRef", true); RESULT.put("ids", a); RESULT.put("type", b); :}
                        | RW_VAR IDENTIFIER_LIST:a COLON ARRAY_TYPE:b {: RESULT = new HashMap<>(); RESULT.put("byRef", true); RESULT.put("ids", a); RESULT.put("type", b.getParentTypeId()); RESULT.put("isArray", true); RESULT.put("isRange", false); RESULT.put("isRecord", false); RESULT.put("minVal", b.getRangeDefinition()[0]); RESULT.put("maxVal", b.getRangeDefinition()[1]); :}
                        | RW_VAR IDENTIFIER_LIST:a COLON RANGE_DEF:b {: RESULT = new HashMap<>(); RESULT.put("byRef", true); RESULT.put("ids", a); RESULT.put("type", TypesTable.DefaultTypes.INTEGER.id); RESULT.put("isArray", false); RESULT.put("isRange", true); RESULT.put("isRecord", false); RESULT.put("minVal", b[0]); RESULT.put("maxVal", b[1]); :}
//                        | RW_VAR IDENTIFIER_LIST COLON /*RW_RECORD*/ ID
                        | RW_VAR IDENTIFIER_LIST:a COLON ID:b {: RESULT = new HashMap<>(); RESULT.put("byRef", true); RESULT.put("ids", a); RESULT.put("type", b); RESULT.put("isArray", false); RESULT.put("isRange", false); RESULT.put("isRecord", false); RESULT.put("minVal", null); RESULT.put("maxVal", null); :}
                        ;

/*
* functions/procedures calls
*/

CALL_TO_METHOD ::= ID OPENPAR CALL_ARGUMENTS CLOSEPAR
                 ;

CALL_ARGUMENTS ::= CALL_ARGUMENTS COMMA EXPRESSION
                | EXPRESSION
                | /* empty */
                ;

ARGUMENT ::= EXPRESSION
              ;

/*
reserved functions (writeln and readln)
*/
WRITELN_CALL ::= RW_WRITELN OPENPAR CALL_ARGUMENTS CLOSEPAR
               ;

READLN_CALL ::= RW_READLN OPENPAR CALL_ARGUMENTS CLOSEPAR
                ;

/*
expressions handling
*/

EXPRESSION ::=
             // Primitive values
             ID:a {: RESULT = new SymbolAccess(a, aleft, aright); :}
             | INTEGER:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.INTEGER.id, Integer.parseInt(a)); :}
             | DECIMAL:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.REAL.id, Double.parseDouble(a)); :}
             | CHAR:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.CHARACTER.id, (int)a.charAt(0)); :}
             | STRING:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.STRING.id, a); :}
             | RW_TRUE:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.BOOLEAN.id, true); :}
             | RW_FALSE:a {: RESULT = new Primitive(aleft, aright, TypesTable.DefaultTypes.BOOLEAN.id, false); :}

             // Arithmetic operations
             | EXPRESSION:a PLUS EXPRESSION:b {: RESULT = new Arithmetic(a, b, ArithmeticOperators.SUM, aleft, aright); :}
             | EXPRESSION:a MINUS EXPRESSION:b {: RESULT = new Arithmetic(a, b, ArithmeticOperators.SUB, aleft, aright); :}
             | EXPRESSION:a ASTERISK EXPRESSION:b {: RESULT = new Arithmetic(a, b, ArithmeticOperators.MUL, aleft, aright); :}
             | EXPRESSION:a RW_DIV EXPRESSION:b {: RESULT = new Arithmetic(a, b, ArithmeticOperators.DIV, aleft, aright); :}
             | EXPRESSION:a RW_MOD EXPRESSION:b {: RESULT = new Arithmetic(a, b, ArithmeticOperators.MOD, aleft, aright); :}

             | MINUS EXPRESSION:a {: RESULT = new Arithmetic(a, ArithmeticOperators.NEG, aleft, aright); :} %prec UNIT_MINUS

             // Logical operations
             | RW_NOT EXPRESSION:a {: RESULT = new Logical(a, LogicalOperators.NOT, aleft, aright); :} %prec RW_NOT
             | EXPRESSION:a RW_AND EXPRESSION:b {: RESULT = new Logical(a, b, LogicalOperators.AND, aleft, aright); :} %prec RW_AND
             | EXPRESSION:a RW_OR EXPRESSION:b {: RESULT = new Logical(a, b, LogicalOperators.OR, aleft, aright); :} %prec RW_OR
             | EXPRESSION:a RW_AND_THEN EXPRESSION:b {: RESULT = new Logical(a, b, LogicalOperators.ANDTHEN, aleft, aright); :} %prec RW_AND_THEN
             | EXPRESSION:a RW_OR_ELSE EXPRESSION:b {: RESULT = new Logical(a, b, LogicalOperators.ORELSE, aleft, aright); :} %prec RW_OR_ELSE

             // Comparison operations
             | EXPRESSION:a EQUALS EXPRESSION:b {: RESULT = new Comparison(a, b, ComparisonOperators.EQUALS, aleft, aright); :}
             | EXPRESSION:a LESS EXPRESSION:b {: RESULT = new Comparison(a, b, ComparisonOperators.LESS, aleft, aright); :}
             | EXPRESSION:a GREATER EXPRESSION:b {: RESULT = new Comparison(a, b, ComparisonOperators.GREATER, aleft, aright); :}
             | EXPRESSION:a LESSEQUAL EXPRESSION:b {: RESULT = new Comparison(a, b, ComparisonOperators.LESS_EQUALS, aleft, aright); :}
             | EXPRESSION:a GREATEREQUAL EXPRESSION:b {: RESULT = new Comparison(a, b, ComparisonOperators.GREATER_EQUALS, aleft, aright); :}
             | EXPRESSION:a LTGT EXPRESSION:b {: RESULT = new Comparison(a, b, ComparisonOperators.NOT_EQUALS, aleft, aright); :}

             // Call to method
             | CALL_TO_METHOD

             // Parenthesis
             | OPENPAR EXPRESSION:a CLOSEPAR {: RESULT = a; :}
             ;

